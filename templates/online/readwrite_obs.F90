!> This file contains routines for writing and reading
!! synthetic observations that are generated by PDAF. 
!! For the files the netCDF file format is used. 
!! Usually can can use these routines without modifications.

!-----------------------------------------------------------
!>  Initialize observation file
!!
!! This routine initializes the file holding synthetic 
!! observations generated by PDAF.
!!
!! The routine should only be called by a single filter
!! process of only one file is generated. If a separate file
!! for each process shoul dbe generated, each process has to
!! call this routine.
!!
!! __Revision history:__
!! * 2019-01 - Lars Nerger - Initial code
!! * Later revisions - see svn log
!!
SUBROUTINE init_file_syn_obs(dim_obs_max, file_obs, screen)

  USE netcdf        ! Netcdf binary format

  IMPLICIT NONE

! *** Arguments ***
  INTEGER, INTENT(in) :: dim_obs_max       !< maximum dimension of full observation vector
  CHARACTER(len=*), INTENT(in) :: file_obs !< Name of observation file
  INTEGER, INTENT(in) :: screen            !< Whether to print output


! *** Local variables ***
  INTEGER :: i, s                               ! Counters
  INTEGER :: dimid_iter, dimid_one, dimid_obs   ! IDs for dimensions
  INTEGER :: id_time, id_obs, id_var            ! IDs for variables
  INTEGER :: dimids(2)                          ! Array of IDs
  INTEGER :: stat(100)                          ! Status array
  INTEGER :: fileid                             ! netcdf file id
  CHARACTER(len=150) :: attstr                  ! String for attribute


  ! Print name file observation file
  IF (screen>0) THEN
     WRITE (*,'(/5x,a,a)') 'GENOBS: Initialize file for synthetic observations: ',TRIM(file_obs)
  END IF

! *** Initialize file
  s = 1
  stat(s) = NF90_CREATE(file_obs, 0, fileid) 

  attstr  = 'Synthetic observations'
  s = s + 1
  stat(s) = NF90_PUT_ATT(fileid, NF90_GLOBAL, 'title', TRIM(attstr)) 

  ! Define dimensions
  s = s + 1
  stat(s) = NF90_DEF_DIM(fileid, 'dim_obs_max', dim_obs_max, dimid_obs)
  s = s + 1
  stat(s) = NF90_DEF_DIM(fileid, 'one',  1, dimid_one)
  s = s + 1
  stat(s) = NF90_DEF_DIM(fileid, 'steps',  NF90_UNLIMITED, dimid_iter)

  ! Define variables
  s = s + 1
  stat(s) = NF90_DEF_VAR(fileid, 'time', NF90_DOUBLE, dimid_iter, Id_time) 

  dimids(1) = DimId_obs
  dimids(2) = dimid_iter

  s = s + 1
  stat(s) = NF90_DEF_VAR(fileid, 'obs', NF90_DOUBLE, dimids, Id_obs)
  s = s + 1
  stat(s) = NF90_DEF_VAR(fileid, 'dim_obs_f', NF90_INT, dimids(2), Id_var)
  s = s + 1
  stat(s) = NF90_ENDDEF(fileid) 

  DO i = 1,  s
     IF (stat(i) /= NF90_NOERR) &
          WRITE(*, *) 'NetCDF error in init of observation file, no.', i
  END DO

  ! Close file
  s = s + 1
  stat(s) = NF90_CLOSE(fileid)

END SUBROUTINE init_file_syn_obs
! ---------------------------------------------------
!> Write vector of synthetic observations to file
!!
!! This routine writes observations into a netcdf file
!!
!! The routine should only be called by a single filter
!! process if a single file is used (usually holding
!! all available observations). If separate files
!! for difference processes are used, each of these
!! processes needs to call the routine.
!!
!! __Revision history:__
!! * 2019-01 - Lars Nerger - Initial code
!! * Later revisions - see svn log
!!
SUBROUTINE write_syn_obs(step, file_obs, dim_obs_f, observation_f, screen)

  USE netcdf        ! Netcdf binary format

  IMPLICIT NONE

! *** Arguments ***
  INTEGER, INTENT(in) :: step        !< Currrent time step
  INTEGER, INTENT(in) :: dim_obs_f   !< Dimension of full observation vector
  REAL, INTENT(in)    :: observation_f(dim_obs_f) !< Full observation vector
  CHARACTER(len=*)    :: file_obs    !< Name of observation file
  INTEGER, INTENT(in) :: screen      !< Whether to print output

! *** Local variables ***
  INTEGER :: i, s                         ! Counters
  INTEGER, save :: iter=0                 ! file iteration counter
  INTEGER :: fileid                       ! netcdf-ID of file
  INTEGER :: id_time, id_obs, id_dimobsf  ! IDs for dimensions
  INTEGER :: id_dimobsmax                 ! ID for dimension variable
  INTEGER :: stat(100)                    ! status flag
  INTEGER :: countv(2), startv(2)         ! index arrays for writing

  INTEGER :: dim_obs_max   ! maximum observation dimension in file
  INTEGER :: dim_obs_store ! size of observation vector to be stored, min(dim_obs_max, dim_obs_f)


  
! ************************************************
! *** Configuration                            ***
! ************************************************

  ! Increment file position counter
  iter = iter + 1

  ! Print name file observation file
  IF (screen>0) THEN
     WRITE (*,'(5x,a,i7,a,a)') 'GENOBS: Write synthetic observations at index ', iter, &
          ' to file: ',TRIM(file_obs)
  END IF

! ***********************************************
! *** Write observational information to file ***
! ***********************************************

  ! Open file
  s = 1
  stat(s) = NF90_OPEN(TRIM(file_obs), NF90_WRITE, fileid)

  ! Read maximum number of observations
  s = s + 1
  stat(s) = NF90_INQ_DIMID(fileid, 'dim_obs_max', id_dimobsmax)
  s = s + 1
  stat(s) = NF90_Inquire_dimension(fileid, id_dimobsmax, len=dim_obs_max)
  s = s + 1


  ! Write time
  stat(s) = NF90_INQ_VARID(fileid, 'time', id_time)
  s = s + 1

  startv(1) = iter
  countv(1) = 1
  stat(s) = NF90_PUT_VAR(fileid, id_time, REAL(step), start=startv(1:1))
  s = s + 1

  ! Write dim_obs_f
  stat(s) = NF90_INQ_VARID(fileid, 'dim_obs_f', id_dimobsf)
  s = s + 1

  IF (dim_obs_f<= dim_obs_max) THEN
     dim_obs_store = dim_obs_f
  ELSE
     dim_obs_store = dim_obs_max
     WRITE (*,*) 'WARNING: Truncating observations to maximum observation dimension in file'
  END IF
  startv(1) = iter
  countv(1) = 1
  stat(s) = NF90_PUT_VAR(fileid, id_dimobsf, dim_obs_store, start=startv(1:1))
  s = s + 1

  ! Write vector of observations
  stat(s) = NF90_INQ_VARID(fileid, 'obs', id_obs)
  s = s + 1

  startv(2) = iter
  countv(2) = 1
  startv(1) = 1
  countv(1) = dim_obs_store
  stat(s) = NF90_PUT_VAR(fileid, id_obs, observation_f, start=startv)

  DO i = 1,  s
     IF (stat(i) /= NF90_NOERR) &
          WRITE(*, *) 'NetCDF error in writing observations, no.', i
     IF (stat(i) /= NF90_NOERR) &
          write (*,*)  NF90_STRERROR(stat(i))
  END DO

! *****************
! *** Clean up  ***
! *****************

  ! Close files
  s = s + 1
  stat(s) = NF90_CLOSE(fileid)
  
END SUBROUTINE write_syn_obs
! ---------------------------------------------------
!BOP
!> Read vector of synthetic observations from file
!!
!! This routine writes observations into a netcdf file
!!
!! The routine should only be called by a single filter
!! process if a single file is used (usually holding
!! all available observations). If separate files
!! for difference processes are used, each of these
!! processes needs to call the routine.
!!
!! __Revision history:__
!! * 2019-01 - Lars Nerger - Initial code
!! *Later revisions - see svn log
!!
SUBROUTINE read_syn_obs(file_obs, dim_obs_f, observation_f, offset, screen)

  USE netcdf        ! Netcdf binary format

  IMPLICIT NONE

! *** Arguments ***
  INTEGER, INTENT(in) :: dim_obs_f   !< Dimension of full observation vector
  REAL, INTENT(out)   :: observation_f(dim_obs_f) !< Full observation vector
  CHARACTER(len=*)    :: file_obs    !< Name of observation file
  INTEGER, INTENT(in) :: offset      !< Offset for observation set index
  INTEGER, INTENT(in) :: screen      !< Whether to print output

! *** Local variables ***
  INTEGER :: i, s                         ! Counters
  INTEGER, save :: iter=0                 ! file iteration counter
  INTEGER :: fileid                       ! netcdf-ID of file
  INTEGER :: id_obs                       ! IDs for dimensions
  INTEGER :: id_dimobsmax                 ! ID for dimension variable
  INTEGER :: stat(100)                    ! status flag
  INTEGER :: countv(2), startv(2)         ! index arrays for writing

  INTEGER :: dim_obs_max   ! maximum observation dimension in file
  INTEGER :: dim_obs_store ! size of observation vector to be stored, min(dim_obs_max, dim_obs_f)


  
! ************************************************
! *** Configuration                            ***
! ************************************************

  ! Increment file position counter
  iter = iter + 1

  ! Print name file observation file
  IF (screen>0) THEN
     WRITE (*,'(5x,a,i7,a,a)') 'Read synthetic observations at step ', iter+offset, &
          ' from file: ',TRIM(file_obs)
  END IF

! ***********************************************
! *** Write observational information to file ***
! ***********************************************

  ! Open file
  s = 1
  stat(s) = NF90_OPEN(TRIM(file_obs), NF90_WRITE, fileid)

  ! Read maximum number of observations
  s = s + 1
  stat(s) = NF90_INQ_DIMID(fileid, 'dim_obs_max', id_dimobsmax)
  s = s + 1
  stat(s) = NF90_Inquire_dimension(fileid, id_dimobsmax, len=dim_obs_max)
  s = s + 1

  IF (dim_obs_f<= dim_obs_max) THEN
     dim_obs_store = dim_obs_f
  ELSE
     dim_obs_store = dim_obs_max
     WRITE (*,*) 'WARNING: Truncating observations to maximum observation dimension in file'
  END IF

  ! Write vector of observations
  stat(s) = NF90_INQ_VARID(fileid, 'obs', id_obs)
  s = s + 1

  startv(2) = iter+Offset
  countv(2) = 1
  startv(1) = 1
  countv(1) = dim_obs_store
  stat(s) = NF90_GET_VAR(fileid, id_obs, observation_f, start=startv, count=countv)

  DO i = 1,  s
     IF (stat(i) /= NF90_NOERR) &
          WRITE(*, *) 'NetCDF error in reading observations, no.', i
     IF (stat(i) /= NF90_NOERR) &
          write (*,*)  NF90_STRERROR(stat(i))
  END DO

! *****************
! *** Clean up  ***
! *****************

  ! Close files
  s = s + 1
  stat(s) = NF90_CLOSE(fileid)
  
END SUBROUTINE read_syn_obs
