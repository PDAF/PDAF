! $Id: generate_obs.F90 121 2019-04-30 15:33:37Z ggeppert $
!BOP
!
! !Program: generate_obs --- Generate observations from a model trajectory
!
! !INTERFACE:
PROGRAM generate_obs

! !DESCRIPTION:
! This program generates synthetic observations by adding
! random noise to a model trajectory generated by lorenz_96.
! The trajectory is read from a NetCDF file.

! !USES:
  IMPLICIT NONE

  INCLUDE 'netcdf.inc'
!EOP

! Local variables
  INTEGER :: i, s, iter
  CHARACTER(len=120) :: inpath, outpath
  CHARACTER(len=120) :: infile, outfile
  CHARACTER(len=150) :: ncfile_in, ncfile_out
  CHARACTER(len=150) :: attstr
  INTEGER :: obs_err_type
  INTEGER :: ncid_in, ncid_out
  INTEGER :: id_dim, id_time, id_step, id_state
  INTEGER :: dimid_iter, dimid_one, dimid_state
  INTEGER :: id_obs, id_stderr
  INTEGER :: stat(100)
  INTEGER :: dim, nsteps
  INTEGER :: countv(2), startv(2)
  INTEGER :: dimids(2)
  REAL, ALLOCATABLE :: times(:)
  INTEGER, ALLOCATABLE :: steps(:)
  REAL, ALLOCATABLE :: state(:)
  REAL, ALLOCATABLE :: noise(:)
  REAL, ALLOCATABLE :: noise1(:)
  INTEGER :: iseed(4)
  REAL :: stderr


! ************************************************
! *** Configuration                            ***
! ************************************************

  ! Type of observation error
  obs_err_type = 0  ! (0) Gaussian; (1) Laplace/double exponential

  ! Standard deviation for observation error
  stderr = 2.0

  ! Path to and name of file holding model trajectory
  inpath = '../'
  infile = 'state_l63.nc'

  ! Path to and name of output file holding observations
  outpath = '../'
  outfile = 'obs_l63.nc'

  ! Initialize seed
  iseed(1)=2*200+1
  iseed(2)=2*100+5
  iseed(3)=2*10+7
  iseed(4)=2*30+9
!  iseed(1) = 1000  ! This outcommented seed is used by PDAF when
!  iseed(2) = 2034  ! it generates synthetic observations. Thus, for
!  iseed(3) = 0     ! getting identical results, one need to use this
!  iseed(4) = 3     ! seed also here


! ************************************************
! *** Init                                     ***
! ************************************************

  WRITE (*,'(3x,a)') '***************************************************'
  WRITE (*,'(3x,a)') '*** Generate observations from model trajectory ***'
  WRITE (*,'(3x,a/)') '***************************************************'

  ncfile_in = TRIM(inpath)//TRIM(infile)
  WRITE (*,*) 'Read trajectory from file: ',TRIM(ncfile_in)

  ncfile_out = TRIM(outpath)//TRIM(outfile)
  WRITE (*,*) 'Write observations to file: ',TRIM(ncfile_out)


! ********************************************************
! *** Open trajectory file and read dimensions and IDs ***
! ********************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in), NF_NOWRITE, ncid_in)
  s = s + 1

  ! Get dimensions
  stat(s) = NF_INQ_DIMID(ncid_in, 'dim_state', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in, id_dim, dim)
  s = s + 1
  stat(s) = NF_INQ_DIMID(ncid_in, 'timesteps', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in, id_dim, nsteps)
  s = s + 1

  ! Get variable ID
  stat(s) = NF_INQ_VARID(ncid_in, 'state', id_state)

  ALLOCATE(times(nsteps))
  ALLOCATE(steps(nsteps))

  ! Initialize time and time step arrays
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in, 'time', id_time)
  s = s + 1
  stat(s) = NF_GET_VAR_DOUBLE(ncid_in, id_time, times)
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in, 'step', id_step)
  s = s + 1
  stat(s) = NF_GET_VAR_INT(ncid_in, id_step, steps)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions, no.', i
  END DO

  WRITE (*,'(/1x,a)') 'Dimensions of experiment:'
  WRITE (*,'(5x,1x,a,i10)') 'state dimension', dim
  WRITE (*,'(11x,a,i10)')   'time steps', nsteps
  WRITE (*,'(4x,a,f10.3/)')  'observation error', stderr

  IF (obs_err_type == 0) THEN
     WRITE (*,'(1x, a/)') 'Error distribution: Gaussian'
  ELSE
     WRITE (*,'(1x, a/)') 'Error distribuation: Double-exponential'
  END IF

! ***********************************************
! *** Initialize output file for observations ***
! ***********************************************

  ! *** Initialize file
  s = 1
  stat(s) = NF_CREATE(ncfile_out, 0, ncid_out)

  attstr  = 'Synthetic observations from Lorenz63 experiment'
  s = s + 1
  stat(s) = NF_PUT_ATT_TEXT(ncid_out, NF_GLOBAL, 'title', LEN_TRIM(attstr), &
       TRIM(attstr))

  ! Define dimensions
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'dim_state', dim, dimid_state)
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'one',  1, dimid_one)
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'timesteps',  nsteps-1, dimid_iter)

  ! Define variables
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'stderr', NF_DOUBLE, 1, dimid_one, Id_stderr)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'step', NF_INT, 1, dimid_iter, Id_step)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'time', NF_DOUBLE, 1, dimid_iter, Id_time)

  dimids(1) = DimId_state
  dimids(2) = dimid_iter

  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'obs', NF_DOUBLE, 2, dimids, Id_obs)
  s = s + 1
  stat(s) = NF_ENDDEF(ncid_out)

  ! Write std error
  s = s + 1
  stat(s) = NF_PUT_VAR_DOUBLE(ncid_out, Id_stderr, stderr)

  ! Write times
  s = s + 1
  stat(s) = NF_PUT_VAR_DOUBLE(ncid_out, Id_time, times(2:nsteps))

  ! Write time steps
  s = s + 1
  stat(s) = NF_PUT_VAR_INT(ncid_out, Id_step, steps(2:nsteps))

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in init of output file, no.', i
  END DO


! ******************************************************
! *** Read trajectory data and generate observations ***
! ******************************************************

  ! Allocate fields
  ALLOCATE(state(dim))
  ALLOCATE(noise(dim))
  ALLOCATE(noise1(dim))

  loopsteps: DO iter = 2, nsteps

     WRITE (*,'(1x,a,es10.3,a,i6)') &
          'Generate observations for time', times(iter), ' step ', steps(iter)

     ! Read state vector
     startv(2) = iter
     countv(2) = 1
     startv(1) = 1
     countv(1) = dim
     stat(1) = NF_GET_VARA_DOUBLE(ncid_in, id_state, startv, countv, state)
     IF (stat(1) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading state'

     IF (obs_err_type == 0) THEN
       ! Generate random Gaussian noise
        CALL dlarnv(3, iseed, dim, noise)

       ! Add noise to generate observations
        state = state + stderr * noise
     ELSE
        ! Generate random Laplace distributed noise
        CALL dlarnv(1, iseed, dim, noise1)

        DO i = 1, dim
           noise(i) = sign(1.0, (noise1(i)-0.5)) * log(1.0 - 2.0*ABS(noise1(i)-0.5))
        END DO

        ! Add noise to generate observations
        state = state + sqrt(stderr/2.0) * noise
     END IF

     ! Write array of observations
     startv(2) = iter-1
     countv(2) = 1
     startv(1) = 1
     countv(1) = dim
     stat(1) = NF_PUT_VARA_DOUBLE(ncid_out, id_obs, startv, countv, state)
     IF (stat(1) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in writing observation'

  END DO loopsteps

! *****************
! *** Clean up  ***
! *****************

  ! Close files
  s = s + 1
  stat(s) = nf_close(ncid_in)
  s = s + 1
  stat(s) = nf_close(ncid_out)

  ! Deallocate arrays
  DEALLOCATE(times, state, noise, noise1)

  WRITE (*,'(1x,a/)') '------- END -------------'

END PROGRAM generate_obs
