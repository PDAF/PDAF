! $Id: generate_covar.F90 1604 2016-05-30 06:42:16Z lnerger $
!BOP
!Modified by QT 2017-12-12 ---- for AWI-CM 
!Modified again by TQ 2017-12-19

! !Program: generate_covar --- Compute covariance matrix from state trajectory
PROGRAM generate_covar

! !DESCRIPTION:
! This programm to computes a covariance matrix from a
! model trajectory of AWI-CM. The matrix is decomposed in
! EOFs and stored in form of eigenvalues and eigenvectors.
!
! The matrix is generated by a singular value decomposition
! of the perturbation matrix of a long state trajectory
! about its long time mean.
!
! In this version:
! 1) the state vector is structured (z(ssh) u v w t s)^T, where SSH is put on
! top position;
! 2) output frequency is every 5th day;
! 3) Only one output file, including the running mean, singular values and singular vectors 

! !USES:
  IMPLICIT NONE

  INCLUDE 'netcdf.inc'
!EOP

! Local variables
  INTEGER :: i, j, k, s, iter, maxtimes, rank           ! Counters
  CHARACTER(len=120) :: inpath, outpath                 ! File paths
  CHARACTER(len=120) :: infile_u, infile_v, infile_w    ! File names of velocity --input
  CHARACTER(len=120) :: infile_s, infile_z, infile_t    ! File names of salinity, sea surface height and temperature --input
  CHARACTER(len=120) :: outfile                         ! File names   --output
  CHARACTER(len=150) :: ncfile_in_u, ncfile_in_v, ncfile_in_w    ! File names including path
  CHARACTER(len=150) :: ncfile_in_s, ncfile_in_z, ncfile_in_t    ! File name including path
  CHARACTER(len=150) :: ncfile_out                               ! File name including path
  CHARACTER(len=150) :: attstr                          ! Attribute string for NC (NetCDF) output
  INTEGER :: ncid_in_u, ncid_in_v, ncid_in_w            ! NC file IDs
  INTEGER :: ncid_in_s, ncid_in_z, ncid_in_t            ! NC file IDs
  INTEGER :: ncid_out                                   ! NC files IDs
  INTEGER :: id_dim, id_time, id_state                  ! NC dimension and variable IDs
  INTEGER :: id_u, id_v, id_w                           ! Field variable IDs
  INTEGER :: id_s, id_z, id_t                           ! Field variable IDs
  INTEGER :: id_mu, id_mv, id_mw                        ! Mean field variables IDs
  INTEGER :: id_mz, id_ms, id_mt                        ! Mean field variables IDs
  INTEGER :: id_svdu, id_svdv, id_svdw                  ! Singular vector IDs
  INTEGER :: id_svdz, id_svds, id_svdt                  ! Singular vector IDs
  INTEGER :: id_sigma, id_svec                          ! NC dimension and variable IDs
  INTEGER :: dimid_rank, dimid_state, dimid_one         ! NC dimension and variable IDs
  INTEGER :: dimid_n2d, dimid_n3d,dimid_nfields         ! NC dimension and varible IDs
  INTEGER :: dimid_maxtimes                             ! NC dimension and variable IDs
  INTEGER :: n2d, n3d                                   ! Number of 2d nodes and 3d nodes
  INTEGER :: steps, dim_state                           ! Dimensions
  INTEGER :: stat(100)                                  ! Status for NC operations
  INTEGER :: countv(2), startv(2)                       ! Vectors for NC operations
  INTEGER :: dimids(2)                                  ! Vector for NC operations
  REAL :: limit                                         ! Lower limit for singular values
  INTEGER :: dim_fields(6)                              ! Size of field
  INTEGER :: nfields                                    ! Number of fields
  INTEGER :: offsets(6)                                 ! Offset of fields
  INTEGER :: id_field                                   ! Field
  INTEGER :: hwindow                                    ! Half time window                             
  INTEGER :: stddev(6)                                  ! STDDEV of field
  REAL, ALLOCATABLE :: run_meanstate (:, :)             ! Running mean state
  
  REAL, ALLOCATABLE :: times(:)                 ! Array of times from NC file
  REAL, ALLOCATABLE :: states(:, :)             ! State trajectory
  REAL, ALLOCATABLE :: meanstate(:)             ! Mean state of trajectory
  INTEGER :: status                             ! Status Flag
  REAL,ALLOCATABLE,DIMENSION(:) :: svals        ! field of singular values
  REAL,ALLOCATABLE,DIMENSION(:,:) :: svdU       ! left singular vectors

  ! Controls for PDAF_eofcovar
  INTEGER :: remove_mean  ! (1) Let PDAF_eofcovar compute and subtract the mean state;
                          ! (0) mean already removed from trajectory
  INTEGER :: do_mv        ! (1) to perform multivariate normalization; (0) no normalization



! ************************************************
! *** Configuration                            ***
! ************************************************

  ! Maximum number if time slices to consider
  maxtimes = 73

  ! Set do_mv=1 to activate normalization; 0 to run without normalization
  do_mv = 1   

  ! Path to and name of file holding model trajectory
  inpath = '/gfs1/work/hbkqtang/output/cpl_work_2016_1year_5day/'
  infile_z = 'zos_fesom_20160101.nc'
  infile_u = 'uo_fesom_20160101.nc'
  infile_v = 'vo_fesom_20160101.nc'
  infile_w = 'wo_fesom_20160101.nc'
  infile_t = 'thetao_fesom_20160101.nc'
  infile_s = 'so_fesom_20160101.nc'

  ! Path to and name of output file holding covariance matrix
 outpath = '/gfs1/work/hbkqtang/output/cov_6/'
!   outpath = '/gfs1/work/hzfblner/fesom_cov/'
  outfile = 'cov.nc'

  ! Lower limit for eigenvalue
  limit = 1.0e-12

  ! Number of fields in state vector
  nfields = 6

  ! Half time window for running mean (2*irange+1)
  hwindow = 6

  ! Note: multivariate normalization can be useful if there are several fields
  !       with difference variances. Here, we have six fields.  


! ************************************************
! *** Init                                     ***
! ************************************************

  WRITE (*,'(10x,a)') '*******************************************'
  WRITE (*,'(10x,a)') '*             GENERATE_COVAR              *'
  WRITE (*,'(10x,a)') '*                                         *'
  WRITE (*,'(10x,a)') '*    Compute covariance matrix and mean   *'
  WRITE (*,'(10x,a)') '*     state from a sequence of states.    *'
  WRITE (*,'(10x,a)') '*                                         *'
  WRITE (*,'(10x,a)') '*   Write covar matrix as scaled eigen-   *'
  WRITE (*,'(10x,a)') '*    vectors and singular values into     *'
  WRITE (*,'(10x,a)') '*              NetCDF file                *'
  WRITE (*,'(10x,a/)') '*******************************************'

  ncfile_in_z = TRIM(inpath)//TRIM(infile_z)
  WRITE (*,*) 'Read sea surface height trajectory from file: ',TRIM(ncfile_in_z)
  ncfile_in_u = TRIM(inpath)//TRIM(infile_u)
  WRITE (*,*) 'Read u-velocity trajectory from file: ',TRIM(ncfile_in_u)
  ncfile_in_v = TRIM(inpath)//TRIM(infile_v)
  WRITE (*,*) 'Read v-velocity trajectory from file: ',TRIM(ncfile_in_v)
  ncfile_in_w = TRIM(inpath)//TRIM(infile_w)
  WRITE (*,*) 'Read w-velocity trajectory from file: ',TRIM(ncfile_in_w)
  ncfile_in_t = TRIM(inpath)//TRIM(infile_t)
  WRITE (*,*) 'Read temperature trajectory from file: ',TRIM(ncfile_in_t)
  ncfile_in_s = TRIM(inpath)//TRIM(infile_s)
  WRITE (*,*) 'Read salinity trajectory from file: ',TRIM(ncfile_in_s)

  ncfile_out = TRIM(outpath)//TRIM(outfile)
  WRITE (*,*) 'Write output to file: ',TRIM(ncfile_out)  


! ***********************************************************
! *** Open u-velocity trajectory file and read dimensions ***
! ***********************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_u), NF_NOWRITE, ncid_in_u)
  s = s + 1

  ! Get dimensions
  stat(s) = NF_INQ_DIMID(ncid_in_u, 'nodes_3d', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in_u, id_dim, n3d)
  s = s + 1
  stat(s) = NF_INQ_DIMID(ncid_in_u, 'time', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in_u, id_dim, steps)

  ! Initialize time array
  ALLOCATE(times(steps))
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_u, 'time', id_time)
  s = s + 1
  stat(s) = NF_GET_VAR_DOUBLE(ncid_in_u, id_time, times)

  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_u, 'uo', id_u)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of u-velocity file, no.', i
  END DO


! ***********************************************************
! *** Open v-velocity trajectory file and read dimensions ***
! ***********************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_v), NF_NOWRITE, ncid_in_v)
 
  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_v, 'vo', id_v)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of v-velocity file, no.', i
  END DO


! ***********************************************************
! *** Open w-velocity trajectory file and read dimensions ***
! ***********************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_w), NF_NOWRITE, ncid_in_w)
 
  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_w, 'wo', id_w)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of w-velocity file, no.', i
  END DO


! *******************************************************************
! *** Open sea surface height trajectory file and read dimensions ***
! *******************************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_z), NF_NOWRITE, ncid_in_z)
  s = s + 1

  ! Get dimensions
  stat(s) = NF_INQ_DIMID(ncid_in_z, 'nodes_2d', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in_z, id_dim, n2d)

  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_z, 'zos', id_z)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of sea surface height file, no.', i
  END DO


! ***********************************************************
! *** Open salinity trajectory file and read dimensions ***
! ***********************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_s), NF_NOWRITE, ncid_in_s)
 
  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_s, 'so', id_s)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of salinity file, no.', i
  END DO


! ******************************************************************
! *** Open ocean temperature trajectory file and read dimensions ***
! ******************************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_t), NF_NOWRITE, ncid_in_t)
 
  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_t, 'thetao', id_t)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of ocean temperature file, no.', i
  END DO


  ! Write dimensions
  WRITE (*,'(/1x,a)') 'Dimensions of experiment:'
  WRITE (*,'(10x,1x,a3,i12)') 'n2d', n2d
  WRITE (*,'(10x,1x,a3,i12)') 'n3d', n3d
  WRITE (*,'(5x,2x,a,i12)') 'nfields', nfields
  WRITE (*,'(11x,a,i10)')   'time steps', steps

  IF (steps < maxtimes) THEN
     WRITE (*,'(1x,a)') '!! Number of available time slices is smaller than maxtimes - resetting!'
     maxtimes = steps
  END IF
  WRITE (*,'(13x,a8,i10)') 'maxtimes', maxtimes


 ! Define state dimension
  dim_state = 5 * n3d + n2d

  WRITE (*,'(5x,a,i12)') 'dim_state', dim_state

  ! Define offsets
  offsets (1) = 0                ! offset of field SSH
  offsets (2) = n2d              ! offset of field u
  offsets (3) = n2d + n3d        ! offset of field v
  offsets (4) = n2d + 2 * n3d    ! offset of field w
  offsets (5) = n2d + 3 * n3d    ! offset of field t
  offsets (6) = n2d + 4 * n3d    ! offset of field s

    

! ****************************
! *** Read trajectory data ***
! ****************************

  WRITE (*,'(/1x,a)') '------- Read trajectory -------------'
  ALLOCATE(states(dim_state, maxtimes))
  
  read_in: DO iter = 1, maxtimes
 
  ! Read SSH
  startv(2) = iter
  countv(2) = 1
  countv(1) = n2d
  startv(1) = 1  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_z, id_z, startv, countv, &
      states(1+offsets (1): n2d+offsets (1), iter))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading SSH'
  
  ! Read u
  countv(1) = n3d  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_u, id_u, startv, countv, &
      states(1+offsets (2): n3d+offsets (2), iter))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading u'

  ! Read v
  countv(1) = n3d  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_v, id_v, startv, countv, &
      states(1+offsets (3): n3d+offsets (3), iter))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading v'

  ! Read w
  countv(1) = n3d  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_w, id_w, startv, countv, &
      states(1+offsets (4): n3d+offsets (4), iter))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading w'

  ! Read temperature
  countv(1) = n3d  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_t, id_t, startv, countv, &
      states(1+offsets (5): n3d+offsets (5), iter))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading temperature'

  ! Read salinity
  countv(1) = n3d
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_s, id_s, startv, countv, &
      states(1+offsets (6): n3d+offsets (6), iter))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading salinity'

  END DO read_in

  ! Close trajectory file

  stat(1) = nf_close(ncid_in_z)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing SSH trajectory file'

  stat(1) = nf_close(ncid_in_u)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing u-velocity trajectory file'

  stat(1) = nf_close(ncid_in_v)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing v-velocity trajectory file'

  stat(1) = nf_close(ncid_in_w)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing w-velocity trajectory file'

  stat(1) = nf_close(ncid_in_t)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing temperature trajectory file'

  stat(1) = nf_close(ncid_in_s)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing salinity trajectory file'



! **********************************
! *** Compute running mean state ***
! **********************************

  WRITE (*,'(1x,a,i5,a)') 'Compute running mean over', maxtimes,' snapshots'
  ALLOCATE(meanstate(dim_state))
  ALLOCATE(run_meanstate(dim_state, maxtimes))
  
  DO i = 1, maxtimes
     meanstate = 0.0

     IF (i > hwindow .AND. i <= (maxtimes - hwindow)) THEN
        Do j = i - hwindow, i + hwindow
           meanstate = meanstate + states (:, j)
        END DO

     ELSE IF (i <= hwindow) THEN
        DO j = 1, i + hwindow
           meanstate = meanstate + states (:, j)
        END DO
        DO j = maxtimes - hwindow +i, maxtimes
           meanstate = meanstate + states (:, j)
        END DO

     ELSE IF (i > maxtimes - hwindow) THEN
        DO j = i - hwindow, maxtimes
           meanstate = meanstate + states (:, j)
        END DO        
        DO j = 1, i + hwindow - maxtimes
            meanstate = meanstate + states (:, j)
         END DO
     END IF
     
     meanstate = meanstate / (2 * hwindow + 1)
     run_meanstate (:, i) = meanstate
 
  END DO
  
  ! get residual
  states = states - run_meanstate  



! *********************************************************
! *** Singular value decomposition of covariance matrix ***
! ***                                                   ***
! *** The covariance matrix is given by the state       ***
! *** sequences X of k states as                        ***
! ***          -1    _     _ T        T                 ***
! *** P = (k-1)   (X-X) (X-X)  = U L U  (EVP)           ***
! ***                                                   ***
! *** Thus we compute the singular value decomposition  ***
! ***     _        T            -1    2  T              ***
! ***   X-X = U S V ;  P = (k-1)   U S  U               ***
! ***                                                   ***
! ***                         -1/2                      ***
! *** and we store U and (k-1)     S in a NetCDF file.  ***
! *********************************************************

  WRITE (*,'(/1x,a)') '------- Compute covariance matrix decomposition -------------'

  ! PDAF_eofcovar should compute and subtract the mean state from Trajectory 
  ! before decomposition. Afterwards, it's added again.
  remove_mean = 0

  ! Set some variables only used for multivariate normalization
  dim_fields (1) = n2d  ! Size of field SSH
  dim_fields (2) = n3d  ! Size of field u
  dim_fields (3) = n3d  ! Size of field v
  dim_fields (4) = n3d  ! Size of field w
  dim_fields (5) = n3d  ! Size of field t
  dim_fields (6) = n3d  ! Size of field s

  ! Allocate arrays for singular values and vectors
  ALLOCATE(svals(maxtimes))
  ALLOCATE(svdU(dim_state, maxtimes))

  ! Call routine generating matrix decomposition
  CALL PDAF_eofcovar(dim_state, maxtimes, nfields, dim_fields, offsets, &
       remove_mean, do_mv, states, stddev, svals, svdU, meanstate, 1, status)

 
! *********************************************************
! *** Write mean state and decomposed covariance matrix ***
! *********************************************************

  ! *** Determine rank to write ***

  getlimit: DO i = 1, maxtimes
     IF (svals(i) >= limit) THEN
        rank = i
     ELSE
        EXIT getlimit
     END IF
  END DO getlimit
  IF (rank < maxtimes) THEN
     WRITE (*,'(1x,a,i6,a,es10.2)') &
          'Use maximum of ', rank, ' eigenvectors due to eigenvalue-limit of ',limit
  END IF
  IF (rank == maxtimes) THEN
     rank = maxtimes - 1
     WRITE (*,'(5x,a,i4)') '++ reset rank to ',rank
  END IF

  WRITE (*,'(5x,a)') 'Singular values: '
  DO i = 1, rank
    WRITE (*, '(10x, i4, es12.3)') i, svals(i)
  END DO


  WRITE (*,'(/1x,a)') '------- Write decomposed covariance matrix -------------'

  ! *** Initialize file
  s = 1
  stat(s) = NF_CREATE(ncfile_out, IOR(NF_NOCLOBBER,NF_64BIT_OFFSET), ncid_out)

  IF (do_mv == 1) THEN
     attstr = 'Running mean state, scaled singular vectors and values of multivariate decomposed covariance matrix for AWI-CM'
  ELSE
     attstr = 'Running mean state, singular vectors and values of decomposed covariance matrix for AWI-CM'
  END IF

  s = s + 1
  stat(s) = NF_PUT_ATT_TEXT(ncid_out, NF_GLOBAL, 'title', LEN_TRIM(attstr), &
       TRIM(attstr))

  attstr = 'SSH, U, V, W, T, S'
  s = s + 1
  stat(s) = NF_PUT_ATT_TEXT(ncid_out, NF_GLOBAL, 'state_fields', LEN_TRIM(attstr), &
       TRIM(attstr))

  ! Define dimensions
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'rank',  rank, dimid_rank)
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'nodes_2D', n2d, dimid_n2d)
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'nodes_3D', n3d, dimid_n3d)
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'dim_state', dim_state, dimid_state)
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'one',  1, dimid_one)
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'nfields',  nfields, dimid_nfields)

  ! Define variables
  ! sigular values
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'sigma', NF_DOUBLE, 1, dimid_rank, Id_sigma)
 
  ! mean state
  dimids(1) = DimId_n2d
  dimids(2) = dimid_one
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'ssh_mean', NF_REAL, 2, dimids, Id_mz)

  dimids(1) = DimId_n3d
  dimids(2) = dimid_one
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'u_mean', NF_REAL, 2, dimids, Id_mu)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'v_mean', NF_REAL, 2, dimids, Id_mv)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'wpot_mean', NF_REAL, 2, dimids, Id_mw)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'temp_mean', NF_REAL, 2, dimids, Id_mt)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'salt_mean', NF_REAL, 2, dimids, Id_ms)


  ! singular vectors
  dimids(1) = DimId_n2d
  dimids(2) = dimid_rank
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'ssh_svd', NF_REAL, 2, dimids, Id_svdz)

  dimids(1) = DimId_n3d
  dimids(2) = dimid_rank
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'u_svd', NF_REAL, 2, dimids, Id_svdu)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'v_svd', NF_REAL, 2, dimids, Id_svdv)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'w_svd', NF_REAL, 2, dimids, Id_svdw)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'temp_svd', NF_REAL, 2, dimids, Id_svdt)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'salt_svd', NF_REAL, 2, dimids, Id_svds)


  ! End Define mode
  s = s + 1
  stat(s) = NF_ENDDEF(ncid_out)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in init of output file, no.', i
  END DO

  ! Write singular values
  s = 1
  stat(s) = NF_PUT_VAR_DOUBLE(ncid_out, id_sigma, svals(1:rank))
  
  ! Write running mean (for the last snap shot)
  ! Write SSH part
  startv(2) = 1
  countv(2) = 1
  startv(1) = 1
  countv(1) = n2d  
  s = s + 1
  stat(s) = NF_PUT_VARA_REAL(ncid_out, id_mz, startv, countv, &
          REAL(run_meanstate(1+offsets (1) : n2d+offsets (1), 73), 4))

  ! Write U part
  countv(1) = n3d
  stat(s) = NF_PUT_VARA_REAL(ncid_out, id_mu, startv, countv, &
          REAL(run_meanstate(1+offsets (2) : n3d+offsets (2), 73), 4))

  ! Write V part
  countv(1) = n3d
  s = s + 1
  stat(1) = NF_PUT_VARA_REAL(ncid_out, id_mv, startv, countv, &
          REAL(run_meanstate(1+offsets (3) : n3d+offsets (3), 73), 4))

  ! Write W part
  countv(1) = n3d
  s = s + 1
  stat(1) = NF_PUT_VARA_REAL(ncid_out, id_mw, startv, countv, &
          REAL(run_meanstate(1+offsets (4) : n3d+offsets (4), 73), 4))

  ! Write Temperature part
  countv(1) = n3d
  s = s + 1
  stat(s) = NF_PUT_VARA_REAL(ncid_out, id_mt, startv, countv, &
          REAL(run_meanstate(1+offsets (5) : n3d+offsets (5), 73), 4))

  ! Write Salinity part
  countv(1) = n3d
  s = s + 1
  stat(s) = NF_PUT_VARA_REAL(ncid_out, id_ms, startv, countv, &
          REAL(run_meanstate(1+offsets (6) : n3d+offsets (6), 73), 4))

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in writing running mean state, no.', i
  END DO


  ! *** Write singular vectors

  writevectors: DO i = 1, rank

     ! Write SSH part
     startv(2) = i
     countv(2) = 1
     startv(1) = 1
     countv(1) = n2d
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdz, startv, countv, &
          REAL(svdU(1+offsets (1) : n2d+offsets (1), i), 4))

     ! Write U part
     countv(1) = n3d
     s = 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdu, startv, countv, &
          REAL(svdU(1+offsets (2) : n3d+offsets (2), i), 4))

     ! Write V part
     countv(1) = n3d
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdv, startv, countv, &
          REAL(svdU(1+offsets (3) : n3d+offsets (3), i), 4))

     ! Write W part
     countv(1) = n3d
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdw, startv, countv, &
          REAL(svdU(1+offsets (4) : n3d+offsets (4), i), 4))

     ! Write Temperature part
     countv(1) = n3d
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdt, startv, countv, &
          REAL(svdU(1+offsets (5) : n3d+offsets (5), i), 4))

     ! Write Salinity part
     countv(1) = n3d
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svds, startv, countv, &
          REAL(svdU(1+offsets (6) : n3d+offsets (6), i), 4))

     DO k = 1,  s
        IF (stat(k) /= NF_NOERR) &
             WRITE(*, *) 'NetCDF error in writing singular vectors, no.', k,' rank',i
     END DO

  END DO writevectors

  ! Close file
  s = 1
  stat(s) = NF_CLOSE(ncid_out)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in singular vectors to output file, no.', i
  END DO


! ********************
! *** Finishing up ***
! ********************

  DEALLOCATE(states, meanstate)
  DEALLOCATE(svals, svdU)

  WRITE (*,'(/1x,a/)') '------- END -------------'

END PROGRAM generate_covar



















